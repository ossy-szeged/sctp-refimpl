AC_INIT(src/main.c)
AM_INIT_AUTOMAKE(enrp-server, 0.1.1)

AC_PROG_CC

AC_CANONICAL_HOST

CFLAGS="$CFLAGS -Wall -pedantic -ansi"

case $host_os in
solaris*)
    CFLAGS="$CFLAGS -D_XPG4_2"
    ;;
linux*)
    CFLAGS="$CFLAGS -D_BSD_SOURCE -Wno-variadic-macros"
    ;;
darwin*)
    CFLAGS="$CFLAGS -Wno-variadic-macros"
    ;;
freebsd7*)
    CFLAGS="$CFLAGS -Wno-variadic-macros"
    ;;
esac

AC_CHECK_TYPE(size_t)
AC_CHECK_TYPE(ssize_t)


AC_CHECK_FUNCS(socket, , AC_CHECK_LIB(socket, socket))
AC_CHECK_FUNCS(inet_addr, , AC_CHECK_LIB(nsl, inet_addr))
AC_CHECK_FUNCS(sctp_sendmsg, , AC_CHECK_LIB(sctp, sctp_sendmsg))
AC_CHECK_FUNCS(sctp_sendx, , AC_CHECK_LIB(sctp, sctp_sendx))

AC_CHECK_HEADERS(sys/types.h)

AC_CHECK_MEMBER(
	 struct sockaddr_in.sin_len,
	 AC_DEFINE(HAVE_SIN_LEN, 1, [Define this if your IPv4 has sin_len in sockaddr_in struct.]),,
	[#ifdef HAVE_SYS_TYPES_H
	 #include <sys/types.h>
	 #endif
	 #include <netinet/in.h>])

AC_CHECK_MEMBER(
	 struct sockaddr_in6.sin6_len,
	 AC_DEFINE(HAVE_SIN6_LEN, 1, [Define this if your IPv6 has sin6_len in sockaddr_in6 struct.]),,
	[#ifdef HAVE_SYS_TYPES_H
	 #include <sys/types.h>
	 #endif
	 #include <netinet/in.h>])

AC_MSG_CHECKING(for socklen_t)
AC_TRY_COMPILE(
	[#ifdef HAVE_SYS_TYPES_H
	 #include <sys/types.h>
	 #endif
	 #include <sys/socket.h>],
	[socklen_t x = 1; return x;],
	[AC_MSG_RESULT(yes)],
	[AC_MSG_RESULT(int)
	 AC_DEFINE(socklen_t, int, [Define a type for socklen_t.])]) 


AC_MSG_CHECKING(for SCTP_SENDALL)
AC_TRY_COMPILE(
	[#ifdef HAVE_SYS_TYPES_H
	 #include <sys/types.h>
	 #endif
	 #include <netinet/sctp.h>],
	[struct sctp_sndrcvinfo info;
	 info.sinfo_flags = SCTP_SENDALL;
	 return 0;],
	[AC_MSG_RESULT(yes)
	 AC_DEFINE(HAVE_SCTP_SENDALL, 1, [Define this if you want to use broadcast via sctp_send])],
	[AC_MSG_RESULT(no)])


AC_MSG_CHECKING(for INADDR_NONE)
AC_TRY_COMPILE(
	[#ifdef HAVE_SYS_TYPES_H
	 #include <sys/types.h>
	 #endif
	 #include <arpa/inet.h>
	 #include <netinet/in.h>
	 #include <sys/socket.h>],
	[int x = INADDR_NONE; return 0;],
	[AC_MSG_RESULT(yes)],
	[AC_MSG_RESULT(-1)
	 AC_DEFINE(INADDR_NONE, -1, [Define -1 for INADDR_NONE.])]) 

AC_OUTPUT(Makefile src/Makefile)
