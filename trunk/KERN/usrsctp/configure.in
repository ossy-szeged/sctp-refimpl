AC_INIT(usrsctplib/netinet/sctp.h)
AM_INIT_AUTOMAKE(udp_examples, 1.0)

AC_PROG_CC
AC_PROG_LIBTOOL
AC_CANONICAL_HOST

CFLAGS="$CFLAGS -D__Userspace__ -DSCTP_SIMPLE_ALLOCATOR"

case $host_os in
darwin*)
  CFLAGS="$CFLAGS -U__APPLE__ -D__Userspace_os_Darwin"
  ;;
linux*)
  CFLAGS="$CFLAGS -D__Userspace_os_Linux"
  ;;
freebsd*)
  CFLAGS="$CFLAGS -U__FreeBSD__ -D__Userspace_os_FreeBSD"
  ;;
solaris*)
  CFLAGS="$CFLAGS -D_XPG4_2"
  ;;
esac

AC_CHECK_TYPE(size_t)
AC_CHECK_TYPE(ssize_t)

AC_CHECK_FUNCS(socket, , AC_CHECK_LIB(socket, socket))
AC_CHECK_FUNCS(inet_addr, , AC_CHECK_LIB(nsl, inet_addr))

AC_CHECK_HEADERS(sys/types.h)
AC_CHECK_MEMBER(struct sockaddr_in.sin_len,
                AC_DEFINE(HAVE_SIN_LEN, 1, [Define this if your IPv4 has sin_len in sockaddr_in struct.]),,
                [#ifdef HAVE_SYS_TYPES_H
                 #include <sys/types.h>
                 #endif
                 #include <netinet/in.h>])

AC_MSG_CHECKING(for socklen_t)
AC_TRY_COMPILE([#ifdef HAVE_SYS_TYPES_H
                #include <sys/types.h>
                #endif
                #include <sys/socket.h>],
               [socklen_t x; x = 1; return 0;],
               [AC_MSG_RESULT(yes)],
               [AC_MSG_RESULT(int)
                AC_DEFINE(socklen_t, int, [Define a type for socklen_t.])]) 

AC_OUTPUT(Makefile usrsctplib/Makefile)
